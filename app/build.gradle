apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    signingConfigs {
        YYBKey {
            keyAlias        project.YYB_KEY_ALIAS
            keyPassword     project.YYB_KEY_PWD
            storeFile       file(project.YYB_FILE_PATH)
            storePassword   project.YYB_STORE_PWD
        }
        KuAnKey {
            keyAlias        project.KUAN_KEY_ALIAS
            keyPassword     project.KUAN_KEY_PWD
            storeFile       file(project.KUAN_FILE_PATH)
            storePassword   project.KUAN_STORE_PWD
        }
    }

    compileSdkVersion 26
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "wxm.dofcalculator"
        minSdkVersion 24
        targetSdkVersion 26
        versionCode 3
        versionName "1.03"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        flavorDimensions "versionCode"
    }

    sourceSets {
        main {
            res.srcDirs =
                    [
                            'src/main/res',
                            'src/main/res/layouts/activities',
                            'src/main/res/layouts/item',
                            'src/main/res/layouts/dlg',
                            'src/main/res/layouts/extend',
                            'src/main/res/layouts'
                    ]
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField "Boolean", "FILL_TESTDATA", "false"
        }

        debug {
            buildConfigField "Boolean", "FILL_TESTDATA", "true"
        }
    }

    productFlavors {
        YYB_keepaccount {
            signingConfig signingConfigs.YYBKey
        }

        KuAn_keepaccount {
            signingConfig signingConfigs.KuAnKey
        }
    }


    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${variant.name}-${variant.versionName}-${releaseTime()}.apk"
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

static def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("GMT+8"))
}

dependencies {
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.0'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    implementation 'com.jakewharton:butterknife:8.8.0'
    implementation 'com.squareup.okhttp3:okhttp:3.7.0'
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'io.reactivex:rxandroid:1.1.0'
    implementation 'io.reactivex:rxjava:1.1.1'
    implementation 'com.trello:rxlifecycle:0.5.0'
    implementation 'com.trello:rxlifecycle-components:0.5.0'

    implementation 'com.jakewharton:kotterknife:0.1.0-SNAPSHOT'

    implementation project(':androidutil')
    implementation project(':uilib')
}


configurations.all {
    resolutionStrategy {
        force 'com.android.support:support-annotations:24.2.1'
    }
}

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}
